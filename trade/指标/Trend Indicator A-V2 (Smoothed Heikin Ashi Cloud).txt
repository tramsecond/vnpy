Trend Indicator A-V2 (Smoothed Heikin Ashi Cloud)


“趋势指标 A-V2”和“趋势指标 B-V2”是我最初趋势指标的更新和改进版本。我们彻底重新思考了代码，在计算中添加了最高价和最低价，并通过配色方案进行了一些自定义。

实际上，该指标使用 EMA 和平均足来提供趋势的整体概念。
“趋势指标A-V2”是显示“平滑平均足”的覆盖图。
“趋势指标B-V2”以不同的方式使用相同的值来衡量趋势的动量并识别潜在的趋势拒绝。


请考虑到这是一个滞后指标。
2021年5月6日
发行说明
添加分辨率自定义
2021年10月18日
发行说明
- 增加了“MA 类型”定制可能性
- 用“基本颜色输入”功能取代了“配色方案”功能（用户友好，易于定制）
- 将标题名称切换为英文以便更好地理解
- 小脚本优化
2021年10月18日
发行说明
-将 ma_period defval 更改为 7
-将 ma_period defval_smoothing 更改为 7
2024年4月23日
发行说明
- 将代码更新至 PineScript 版本 5
- 脚本重大更新（使用函数 + 美化功能优化脚本）
- 移除了通过移动平均线进行的过度平滑，以减少延迟（之前有两个平滑阶段，现在只有一个） -
在“MA 类型”输入字符串中添加了 ALMA、HMA 和 ZLEMA。您现在可以选择：
' ALMA '（Arnaud Legoux 移动平均线）
' EMA '（指数移动平均线）
赫尔移动平均线( HMA )
' SMA '（简单移动平均线）
' SWMA '（对称加权移动平均线）
' VWMA '（成交量加权移动平均线）
' WMA '（加权移动平均线）
' ZLEMA '（零滞后指数移动平均线）

-为想要使用 ALMA 的用户添加了自定义设置。
偏移范围从 1 到 0。偏移量为 1 将使指标精确跟随价格，而接近 0 则会导致更多滞后。默认值设置为 0.85
偏差表示过滤器的范围。值越低，对价格变化的反应越弱。默认值为 6

-默认 MA 周期已更改为 9，以减少错误信号。-
默认 ME 类型设置为“EMA”，因为最常用。



//@version=5
//
//╔════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╗
//║ Author  | DZIV (dzi_v_)
//║ Licence | CC BY-NC-SA 4.0 : https://creativecommons.org/licenses/by-nc-sa/4.0/
//╠════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╣
//║ Changelog :
//║ 2021.04.15 > A-V1.0 -First publish
//║ 2021.05.05 > A-V2.0 -Added high and low calculation and display
//║ 					-Minor script optimisation
//║ 2021.05.06 > A-V2.1 -Added resolution customisation in the study
//║ 2021.10.18 > A-V2.2 -Added "MA Type" customisation possibility
//║ 					-Replaced the "colour schemes" feature by a "base color inputs" feature (user friendly)
//║ 					-Switched the title names to English for a better understanding
//║ 					-Minor script optimisation
//║ 2024.04.21 > A-V2.3 -Switched to PineScript version 5
//║ 					-Major script rethinking and optimisation
//║ 					-Removed double smoothing for less lag
//║ 					-Added ALMA, HMA and ZLEMA to the "MA Type" input string
//╚════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╝
//
//╔════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╗
//║ Settings :
indicator('Trend Indicator A (v2.3)', 'Trend Indicator A (v2.3)', true)

ma_type = input.string('EMA', 'MA Type', ['ALMA','HMA','SMA','SWMA','VWMA','WMA','ZLEMA','EMA'], group = 'Setup')
ma_period = input.int(9, 'MA Period (Length)', 1, group='Setup')

alma_offset = input.float(0.85, 'ALMA Shift', 0, 1, 0.05, group = 'Setup (ALMA)')
alma_sigma = input.int(6, 'ALMA Deviation', 1, step = 1, group = 'Setup (ALMA)')

show_line_1(x) =>
    input.bool(true, 'Show Close line', group = 'On/Off') ? x : na
show_line_2(x) =>
    input.bool(false, 'Show High/Low lines', group = 'On/Off') ? x : na
show_fill(x) =>
    input.bool(true, 'Show fill', group = 'On/Off') ? x : na
//╠════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╣
//║ Calculations :
f(x) =>
    switch ma_type
        'ALMA' => ta.alma(x, ma_period, alma_offset, alma_sigma)
        'HMA' => ta.hma(x, ma_period)
        'SMA' => ta.sma(x, ma_period)
        'SWMA' => ta.swma(x)
        'VWMA' => ta.vwma(x, ma_period)
        'WMA' => ta.vwma(x, ma_period)
        'ZLEMA' => ta.ema(x + x - x[math.floor((ma_period - 1) / 2)], ma_period)
        => ta.ema(x, ma_period)

ma_heikinashi_open = f(request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, open))
ma_heikinashi_close = f(request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, close))
ma_heikinashi_high = f(request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, high))
ma_heikinashi_low = f(request.security(ticker.heikinashi(syminfo.tickerid), timeframe.period, low))

trend = 100 * (ma_heikinashi_close - ma_heikinashi_open) / (ma_heikinashi_high - ma_heikinashi_low)
//╠════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╣
//║ Colors :
color_positive = input.color(color.new(#26A69A, 0), 'Positive color (Bullish)', group = 'Colors')
color_negative = input.color(color.new(#EF5350, 0), 'Negative color (Bearish)', group = 'Colors')
color_neutral = input.color(color.new(#808080, 0), 'Neutral color', group = 'Colors')

color_trend = trend > 0 ? color_positive : color_negative
//╠════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╣
//║ Plot :
plot_open = plot(ma_heikinashi_open, 'Open line', na)
plot_close = plot(ma_heikinashi_close, 'Close line', show_line_1(color_trend), 2)
plot_high = plot(ma_heikinashi_high, 'High line', show_line_2(color_neutral))
plot_low = plot(ma_heikinashi_low, 'Low line', show_line_2(color_neutral))

plot_highest = plot(math.max(ma_heikinashi_open, ma_heikinashi_close),'Highest Body line', na)
plot_lowest = plot(math.min(ma_heikinashi_open, ma_heikinashi_close),'Lowest Body line', na)
//╠════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╣
//║ Fill :
fill(plot_open, plot_close, color.new(color_trend,50), 'Open/Close Cloud')
fill(plot_high, plot_highest, show_fill(color.new(color_neutral,87.5)), title = 'High Cloud')
fill(plot_lowest, plot_low, show_fill(color.new(color_neutral,87.5)), title = 'Low Cloud')
//╚════════════════════════════════════════════════════════════════════════════════════════════════════════════════════╝